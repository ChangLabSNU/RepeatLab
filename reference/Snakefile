GENCODE_RELEASE = '44'
REFERENCE = 'GRCh38.v' + GENCODE_RELEASE
REFERENCE_FASTA_URL = f'https://ftp.ebi.ac.uk/pub/databases/gencode/Gencode_human/release_{GENCODE_RELEASE}/GRCh38.p14.genome.fa.gz'
REFERENCE_ANNOTATION_URL = f'https://ftp.ebi.ac.uk/pub/databases/gencode/Gencode_human/release_{GENCODE_RELEASE}/gencode.v{GENCODE_RELEASE}.annotation.gtf.gz'

rule all:
    input:
        expand(f'{REFERENCE}.genome.{{sufx}}',
               sufx=['fa.gz', 'mm2.idx', 'fa.gz.amb', 'fa.gz.ann',
                     'fa.gz.bwt', 'fa.gz.pac', 'fa.gz.sa', 'fa.gz.fai']),
        expand(f'{REFERENCE}.genome.gtf.gz')

rule download_genome_fasta:
    output: f'{REFERENCE}.genome.fa.gz'
    threads: 10
    shell: 'curl {REFERENCE_FASTA_URL} | zcat | bgzip -c -@ {threads} > {output}'

rule download_genome_gtf:
    output: f'{REFERENCE}.genome.gtf.gz'
    threads: 10
    shell: 'curl {REFERENCE_ANNOTATION_URL} | zcat | bgzip -c -@ {threads} > {output}'

rule build_minimap2_genome_index:
    input: f'{REFERENCE}.genome.fa.gz'
    output: f'{REFERENCE}.genome.mm2.idx'
    shell: 'zcat {input} | minimap2 -x map-ont -k 13 -w 20 -d {output} /dev/stdin'

rule build_bwa_genome_index:
    input: f'{REFERENCE}.genome.fa.gz'
    output:
        expand(f'{REFERENCE}.genome.fa.gz.{{sufx}}', sufx=['amb', 'ann', 'bwt', 'pac', 'sa'])
    shell: 'bwa index {input}'

rule build_samtools_genome_index:
    input: f'{REFERENCE}.genome.fa.gz'
    output: f'{REFERENCE}.genome.fa.gz.fai'
    shell: 'samtools faidx {input}'
